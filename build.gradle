apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testCompile 'ch.qos.logback:logback-core:1.1.2'
    testCompile 'ch.qos.logback:logback-classic:1.1.2'
    testCompile 'org.apache.commons:commons-lang3:3.3.1'
    testCompile 'org.apache.commons:commons-collections4:4.0'
}

group = "net.argius"
archivesBaseName = "minestra"
version = '1.2.0-beta1-SNAPSHOT'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

task showVersionNumber {
    println version
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

compileJava {
    options.compilerArgs += ['-profile', 'compact1']
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { hasProperty("signing.keyId") }
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      def sonatypeUsername = project.hasProperty("sonatypeUsername") ? project.property("sonatypeUsername") : ""
      def sonatypePassword = project.hasProperty("sonatypePassword") ? project.property("sonatypePassword") : ""

      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      pom.project {
        name 'Minestra'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'A small utilities for Java8.'
        url 'http://argius.github.io/minestra/'

        scm {
          connection 'scm:git:https://github.com/argius/minestra.git'
          developerConnection 'scm:git:https://github.com/argius/minestra.git'
          url 'https://github.com/argius/minestra'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'argius'
            name 'argius'
            email 'argius.net@gmail.com'
          }
        }
      }
    }
  }
}
